apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'

android {
  compileSdkVersion Integer.parseInt(COMPILE_SDK)
  buildToolsVersion BUILD_TOOLS_VERSION


  defaultConfig {
    minSdkVersion Integer.parseInt(MIN_SDK)
    targetSdkVersion Integer.parseInt(TARGET_SDK)
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
  compile 'com.nineoldandroids:library:2.4.0'
  compile 'com.android.support:support-annotations:' + SUPPORT_VERSION
  compile 'com.android.support:appcompat-v7:' + SUPPORT_VERSION
}

//publishing ------------------------------

version = VERSION
group = 'org.baole.materialedittext'


task generateSourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
  classpath += project.files(android.getBootClasspath()
          .join(File.pathSeparator))
}

task generateJavadocsJar(type: Jar) {
  from generateJavadocs.destinationDir
  classifier 'javadoc'
}

generateJavadocsJar.dependsOn generateJavadocs

artifacts {
  archives generateJavadocsJar
  archives generateSourcesJar
}


publishing {
    publications {
        MyPublication(MavenPublication) {
            groupId group
            version version
            artifact "$buildDir/outputs/aar/library-release.aar" //aar artifact you want to publish

            //generate pom nodes for dependencies
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.compile.allDependencies.each { dependency ->
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dependency.group)
                    dependencyNode.appendNode('artifactId', dependency.name)
                    dependencyNode.appendNode('version', dependency.version)
                }
            }
        }
    }
}


bintray {
    user = USER
    key = BINTRAY_KEY
    publications = ['MyPublication']
    pkg {
//        repo = 'generic'
        repo = 'maven'
        name = group

        userOrg = user

        version {
            name = VERSION
            desc = 'Material EditText'
            released  = new Date()
            vcsTag = VERSION
        }

        licenses = ['Apache-2.0']
        vcsUrl = git_url
        websiteUrl = home_url

        labels = ['android', 'material', 'edittext']
        publicDownloadNumbers = true
    }

    configurations = ['archives']
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}